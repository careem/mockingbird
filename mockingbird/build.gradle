apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply from: '../publishing.gradle'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.iosX64, 'iosX64')
        fromPreset(presets.iosArm64, 'iosArm64')
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                // `.toString` required to avoid the following error when building for macOS:
                // org.codehaus.groovy.runtime.GStringImpl cannot be cast to java.lang.String
                freeCompilerArgs += "-Xplugin=${project.rootDir}/mockingbird-mock-plugin/build/libs/mockingbird-mock-plugin-1.2.0-SNAPSHOT-all.jar".toString()
            }
        }
    }

    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation Deps.kotlin.stdlib.common
                implementation Deps.kotlin.stdlib
                implementation Deps.kotlinx.atomicfu.common
                implementation Deps.touchlab.stately.isolate.common
                implementation kotlin('test')
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                implementation Deps.kotlin.stdlib.jdk8
                implementation Deps.kotlinx.atomicfu.jvm
                compileOnly "io.arrow-kt:arrow-annotations:0.10.5"
                // To run :create-plugin:shadowJar when building this project
                compileOnly project(path: ':mockingbird-mock-plugin', configuration: 'shadow')
            }
        }

        iosX64Main {
            dependencies {
                implementation Deps.kotlinx.atomicfu.native
            }
        }

        iosArm64Main {
            dependsOn iosX64Main
        }

        commonTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }

        all {
            group GROUP
            version VERSION

            languageSettings {
                progressiveMode = true
                useExperimentalAnnotation('kotlin.Experimental')
            }
        }

    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task sourcesJar(type: Jar) {
    baseName = "mockingbird"
    classifier = 'sources'
    from kotlin.sourceSets.commonMain.kotlin.srcDirs, kotlin.sourceSets.jvmMain.kotlin.srcDirs
}

artifacts {
    archives sourcesJar
}