/**
 *
 * Copyright Careem, an Uber Technologies Inc. company
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'jacoco'

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

subprojects {
    apply plugin: 'jacoco'
}

def mainSubProject = "mockingbird"

task jacocoTestReport(type: JacocoReport,
        dependsOn: []) {
    reports {
        xml.enabled = true
        html.enabled = true
        xml.destination = file("${project(":${mainSubProject}").buildDir}/reports/jacoco/jacocoUnitTestReport.xml")
        html.destination = file("${project(":${mainSubProject}").buildDir}/reports/jacoco/html")
    }

    def srcDirectoryList = []
    def classDirectoryList = []
    def executionDataList = []

    def classPathExcludeList = [
            '**/*Test*.*',
            '**/*Mock*.*',
            '**/*Spy*.*',
            '**/Helpers*.*'
    ]

    subprojects.findAll { it.name != 'app' }.forEach { subProject ->

        //common main directory
        def commonMainSourceDirs = "$subProject.projectDir/src/commonMain/kotlin"
        //Jvm directory (main)
        def jvmMainSourceDirs = "$subProject.projectDir/src/jvmMain/kotlin"
        def kotlinDebugPath = "${subProject.buildDir}/classes/kotlin/jvm"

        def classDirectories = fileTree(
                dir: kotlinDebugPath,
                excludes: classPathExcludeList
        )
        def executionData = fileTree(dir: subProject.buildDir, includes: ['jacoco/jvmTest.exec'])

        srcDirectoryList.addAll(commonMainSourceDirs, jvmMainSourceDirs)
        classDirectoryList.add(classDirectories)
        executionDataList.add(executionData)
    }

    classDirectories.from = files(classDirectoryList)
    executionData.from = files(executionDataList)
    sourceDirectories.from = files(srcDirectoryList)
}