/**
 *
 * Copyright Careem, an Uber Technologies Inc. company
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'maven-publish'

def getReleaseRepositoryUrl() {
    return hasProperty('careem_artifactory_url') ? careem_artifactory_url : System.env.careem_artifactory_url
}

def getSnapshotRepositoryUrl() {
    return hasProperty('careem_artifactory_snapshot_url') ? careem_artifactory_snapshot_url : System.env.careem_artifactory_snapshot_url
}

def getRepositoryUsername() {
    return hasProperty('careem_artifactory_username') ? careem_artifactory_username : System.env.careem_artifactory_username
}

def getRepositoryPassword() {
    return hasProperty('careem_artifactory_api_key') ? careem_artifactory_api_key : System.env.careem_artifactory_api_key
}

publishing {
    publications.all {
        pom.withXml {
            def root = asNode()

            root.children().last() + {
                resolveStrategy = Closure.DELEGATE_FIRST

                name POM_NAME
                description POM_DESCRIPTION

                scm {
                    url POM_SCM_URL
                }
                developers {
                    developer {
                        organization POM_DEVELOPER_ORG
                        organizationUrl POM_DEVELOPER_URL
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username getRepositoryUsername()
                password getRepositoryPassword()
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                println("publishing snapshot for $project.name version $project.version")
                url getSnapshotRepositoryUrl()
            } else {
                println("publishing for $project.name version $project.version")
                url getReleaseRepositoryUrl()
            }
        }
    }
}
