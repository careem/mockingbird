/**
 *
 * Copyright Careem, an Uber Technologies Inc. company
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply from: 'jacoco.gradle'

buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }

        maven {
            url 'https://artifactory-pro.careem-internal.com/artifactory/mobile-libs-release'
            credentials {
                username project.hasProperty('careem_artifactory_username') ?
                        careem_artifactory_username : System.env.careem_artifactory_username
                password project.hasProperty('careem_artifactory_api_key') ?
                        careem_artifactory_api_key : System.env.careem_artifactory_api_key
            }
            content {
                includeGroup "com.careem.mockingbird"
            }
            mavenContent {
                releasesOnly()
            }
        }
    }
    dependencies {
        classpath "com.careem.mockingbird:mockingbird-compiler:1.11.0-SNAPSHOT"  // FIXME I want to depend on the module directly not on published version
        classpath libs.kotlinx.atomicfu.gradle
        classpath libs.jacoco.jacoco
    }
}

allprojects {
    group GROUP
    version VERSION

    repositories {
        mavenCentral()
        google()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
// FIXME I want warnings as errors but I'm getting gradle and kotlin classpatch conflict error
//    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
//        kotlinOptions.allWarningsAsErrors = true
//    }
}

void setupMultiplatformLibrary(Project project, Boolean shouldPublish = true) {
    project.apply plugin: 'org.jetbrains.kotlin.multiplatform'
    if (shouldPublish) {
        project.apply from: '../publishing.gradle'
    }

    project.group = GROUP
    project.version = VERSION

    project.kotlin {
        sourceSets {
            commonTest {
                dependencies {
                    implementation libs.kotlin.test
                    implementation libs.kotlin.test.annotations
                }
            }
        }
    }
}

void setupAllTargetsWithDefaultSourceSets(Project project) {
    setupIosTargets(project)
    setupJvmTarget(project)
    setupJsTarget(project)

    project.kotlin {
        sourceSets {
            jvmJsCommonMain.dependsOn commonMain
            jvmJsCommonTest.dependsOn commonTest

            jvmMain.dependsOn jvmJsCommonMain
            jvmTest.dependsOn jvmJsCommonTest

            jsMain.dependsOn jvmJsCommonMain
            jsTest.dependsOn jvmJsCommonTest
        }
    }
}

private void setupIosTargets(Project project) {
    project.kotlin {
        targets.fromPreset(presets.iosX64, 'iosX64')
        targets.fromPreset(presets.iosArm64, 'iosArm64')
    }
}

private void setupJvmTarget(Project project) {
    project.kotlin {
        targets.fromPreset(presets.jvm, 'jvm')

        sourceSets {
            jvmMain {
                dependencies {

                }
            }

            jvmTest {
                dependencies {
                    implementation libs.junit.junit
                }
            }
        }
    }
}

private void setupJsTarget(Project project) {
    project.apply plugin: JsPlugin
}