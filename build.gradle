import org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile

apply from: 'jacoco.gradle'

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath Deps.kotlin.plugin
        classpath Deps.jacoco
    }
}

allprojects {
    group GROUP
    version VERSION

    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

void setupMultiplatformLibrary(Project project, Boolean shouldPublish = true) {
    project.apply plugin: 'org.jetbrains.kotlin.multiplatform'
    if (shouldPublish) {
        project.apply from: '../publishing.gradle'
    }

    project.group = GROUP
    project.version = VERSION

    project.kotlin {
        sourceSets {
            commonTest {
                dependencies {
                    implementation Deps.kotlin.test
                    implementation Deps.kotlin.test.annotationsCommon
                }
            }
        }
    }
}

void setupAllTargetsWithDefaultSourceSets(Project project) {
    setupIosTargets(project)
    setupJvmTarget(project)
    setupJsTarget(project)

    project.kotlin {
        sourceSets {
            jvmJsCommonMain.dependsOn commonMain
            jvmJsCommonTest.dependsOn commonTest

            jvmMain.dependsOn jvmJsCommonMain
            jvmTest.dependsOn jvmJsCommonTest

            jsMain.dependsOn jvmJsCommonMain
            jsTest.dependsOn jvmJsCommonTest
        }
    }
}

private void setupIosTargets(Project project) {
    project.kotlin {
        targets.fromPreset(presets.iosX64, 'iosX64')
        targets.fromPreset(presets.iosArm64, 'iosArm64')
    }
}

private void setupJvmTarget(Project project) {
    project.kotlin {
        targets.fromPreset(presets.jvm, 'jvm')

        sourceSets {
            jvmMain {
                dependencies {

                }
            }

            jvmTest {
                dependencies {
                    implementation Deps.kotlin.test.junit
                    implementation kotlin('test-junit')
                }
            }
        }
    }
}

subprojects {

    def javaVersion = JavaVersion.VERSION_1_8

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = javaVersion
        task.targetCompatibility = javaVersion
    }

    tasks.withType(KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = javaVersion.toString()
        }
    }
}

private void setupJsTarget(Project project) {
    project.apply plugin: JsPlugin
}