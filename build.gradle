/**
 *
 * Copyright Careem, an Uber Technologies Inc. company
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply from: 'jacoco.gradle'

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath Deps.kotlin.plugin
        classpath Deps.jacoco
    }
}

allprojects {
    group GROUP
    version VERSION

    repositories {
        mavenCentral()
        google()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
    }
}

void setupMultiplatformLibrary(Project project, Boolean shouldPublish = true) {
    project.apply plugin: 'org.jetbrains.kotlin.multiplatform'
    if (shouldPublish) {
        project.apply from: '../publishing.gradle'
    }

    project.group = GROUP
    project.version = VERSION

    project.kotlin {
        sourceSets {
            commonTest {
                dependencies {
                    implementation Deps.kotlin.test
                    implementation Deps.kotlin.test.annotationsCommon
                }
            }
        }
    }
}

void setupAllTargetsWithDefaultSourceSets(Project project) {
    setupIosTargets(project)
    setupJvmTarget(project)
    setupJsTarget(project)

    project.kotlin {
        sourceSets {
            jvmJsCommonMain.dependsOn commonMain
            jvmJsCommonTest.dependsOn commonTest

            jvmMain.dependsOn jvmJsCommonMain
            jvmTest.dependsOn jvmJsCommonTest

            jsMain.dependsOn jvmJsCommonMain
            jsTest.dependsOn jvmJsCommonTest
        }
    }
}

private void setupIosTargets(Project project) {
    project.kotlin {
        targets.fromPreset(presets.iosX64, 'iosX64')
        targets.fromPreset(presets.iosArm64, 'iosArm64')
    }
}

private void setupJvmTarget(Project project) {
    project.kotlin {
        targets.fromPreset(presets.jvm, 'jvm')

        sourceSets {
            jvmMain {
                dependencies {

                }
            }

            jvmTest {
                dependencies {
                    implementation Deps.kotlin.test.junit
                    implementation kotlin('test-junit')
                }
            }
        }
    }
}

private void setupJsTarget(Project project) {
    project.apply plugin: JsPlugin
}